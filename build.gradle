plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'lshdainty'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

jacoco {
    toolVersion = '0.8.13'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    // ==================== Spring Boot Starters ====================
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    // ==================== Security & OAuth ====================
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // ==================== Database & JPA ====================
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.5.1'
    runtimeOnly 'com.h2database:h2'

    // QueryDSL (Hibernate 7 + Jakarta Persistence 3.2)
    implementation 'io.github.openfeign.querydsl:querydsl-jpa:7.1'
    annotationProcessor 'io.github.openfeign.querydsl:querydsl-apt:7.1:jpa'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // P6Spy (SQL 로깅)
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:2.0.0'

    // ==================== Monitoring & Logging ====================
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'com.github.loki4j:loki-logback-appender:2.0.1'

    // ==================== API Documentation ====================
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.0'

    // ==================== Utilities ====================
    implementation 'org.apache.poi:poi:5.3.0'
    implementation 'org.apache.poi:poi-ooxml:5.3.0'
    implementation 'com.github.usingsky:KoreanLunarCalendar:0.3.1'

    // ==================== Lombok ====================
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // ==================== Test ====================
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-data-jpa-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

tasks.named('test') {
    enabled = true
    useJUnitPlatform()
    jvmArgs '-Xmx2g', '-XX:+HeapDumpOnOutOfMemoryError'
    finalizedBy jacocoTestReport
}

def jacocoExcludes = [
    '**/Application*',
    '**/PorestApplication*',
    '**/InitDB*',
    '**/controller/**',    // 추후 제외대상
    '**/controller/dto/**',
    '**/domain/**',
    '**/Q*',
    '**/Config/**',
    '**/config/**',
    '**/lib/**',
    '**/type/**',
    '**/security/**',
    '**/service/dto/**',
    '**/repository/dto/**',
    '**/SessionConst*',
    '**/util/PorestIP*',
    '**/service/EmailService*',
    '**/exception/**',
    // 엑셀 다운로드/복잡한 비즈니스 로직 등으로 테스트 어려움
    '**/service/WorkHistoryServiceImpl$UnregisteredWorkData*',
    '**/service/WorkHistoryServiceImpl*',   // 엑셀 다운로드 메서드가 많은 비중 차지
    '**/service/VacationServiceImpl*'       // 복잡한 휴가 로직으로 단위 테스트 어려움
]

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: jacocoExcludes)
                })
        )
    }
    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: jacocoExcludes)
                })
        )
    }
    violationRules {
        rule {
            enabled = true
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.60
            }
        }
    }
}

// QueryDsl 설정
def querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile
//def querydslDir = "src/main/generated/queryDsl"

sourceSets {
    main.java.srcDirs += [ querydslDir ]
}

tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(querydslDir))
}

clean.doLast {
    file(querydslDir).deleteDir()
}

clean {
    delete file('src/main/generated')
}