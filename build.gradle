plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'lshdainty'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jacoco {
    toolVersion = '0.8.13'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-logging:3.1.0'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.security:spring-security-test'

    // MariaDB
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.1.4'

    // P6Spy 의존성 추가
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.12.0'

    // spring security & oauth
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // email
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    // queryDSL
    implementation 'io.github.openfeign.querydsl:querydsl-jpa:6.10.1'
    annotationProcessor 'io.github.openfeign.querydsl:querydsl-apt:6.10.1:jpa'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // Apache POI
    implementation 'org.apache.poi:poi:5.2.3'
    implementation 'org.apache.poi:poi-ooxml:5.2.3'

    // Springdoc OpenAPI (Swagger UI)
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

    // Prometheus
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'com.github.loki4j:loki-logback-appender:1.5.2'
}

tasks.named('test') {
    enabled = true
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

def jacocoExcludes = [
    '**/Application*',
    '**/PorestApplication*',
    '**/InitDB*',
    '**/controller/**',    // 추후 제외대상
    '**/controller/dto/**',
    '**/domain/**',
    '**/Q*',
    '**/Config/**',
    '**/config/**',
    '**/lib/**',
    '**/type/**',
    '**/security/**',
    '**/service/dto/**',
    '**/repository/dto/**',
    '**/SessionConst*',
    '**/util/PorestIP*',
    '**/service/EmailService*',
    '**/exception/**'
]

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: jacocoExcludes)
                })
        )
    }
    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: jacocoExcludes)
                })
        )
    }
    violationRules {
        rule {
            enabled = true
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.60
            }
        }
    }
}

// QueryDsl 설정
def querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile
//def querydslDir = "src/main/generated/queryDsl"

sourceSets {
    main.java.srcDirs += [ querydslDir ]
}

tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(querydslDir))
}

clean.doLast {
    file(querydslDir).deleteDir()
}

clean {
    delete file('src/main/generated')
}